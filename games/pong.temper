let {...} = import('../rl/raylib.temper');

let abs(v: Int): Int {
  if (v < 0) {
    return -v;
  } else {
    return v;
  }
}

export let main(args: Listed<String>): Void | Bubble {
  var borderSize = 16.0;
  var gameSpeed = 1.0;
  var leftStrength = -0.2;
  var rightStrength = -0.2;
  var maxScore = 11;

  do {
    for (var i = 0; i < args.length; i++) {
      var argBase = args[i].split("=");
      var nextArg = argBase[1] orelse args[++i] orelse "";
      if (argBase[0] == "--border") {
        borderSize = nextArg.toFloat64();
      }
      if (argBase[0] == "--speed") {
        gameSpeed = nextArg.toFloat64();
      }
      if (argBase[0] == "--control") {
        leftStrength = nextArg.toFloat64();
        rightStrength = nextArg.toFloat64();
      }
      if (argBase[0] == "--score") {
        maxScore = nextArg.toInt();
      }
    }
  } orelse do {
    console.log("Wrong arguments!");
    return;
  }

  let UP = 0;
  let DOWN = 1;

  let SCREEN_TITLE = 0;
  let SCREEN_PLAY = 1;
  let SCREEN_ENDING = 2;

  var rightScore = 0;
  var leftScore = 0;

  SetTraceLogLevel(LOG_WARNING);

  InitWindow(0, 0, "Pong!");

  var screen = new Rectangle(0.0, 0.0, GetScreenWidth().toFloat64()/2.0, GetScreenHeight().toFloat64()/2.0);
  var border = new Rectangle(borderSize, borderSize, screen.width - 2.0 * borderSize, screen.height - 2.0 * borderSize);
  var top = new Rectangle(screen.x, screen.y, border.width, border.y);
  var bottom = new Rectangle(screen.x, border.height + borderSize, screen.width, screen.y);

  SetWindowPosition(screen.width.toInt() / 2, screen.height.toInt() / 2);
  SetWindowSize(screen.width.toInt(), screen.height.toInt());
  SetTargetFPS(60);

  var ball = new Rectangle(borderSize * 5.0, border.height, borderSize, borderSize);
  var leftRacket = new Rectangle(border.x + borderSize, border.height / 2.0, borderSize, borderSize * 5.0);
  var rightRacket = new Rectangle(border.width - borderSize, border.height / 2.0, borderSize, borderSize * 5.0);
  var scoreWidth = MeasureText("00", 60);
  var frameTime = 0.0;

  
  let setBallPos(x: Float64, y: Float64): Void {
    ball = new Rectangle(x, y, borderSize, borderSize);
  }

  var xx = borderSize / 2.0;
  var yy = borderSize / 2.0;
  let moveBall(): Void {
    if (CheckCollisionRecs(ball, leftRacket)) {
      if (xx < 0) {
        xx = -xx;
      }
      if (IsKeyDown(KEY_W)) {
        yy += -borderSize * leftStrength;
      }
      if (IsKeyDown(KEY_S)) {
        yy += borderSize * leftStrength;
      }
    } else if (CheckCollisionRecs(ball, rightRacket)) {
      if (xx > 0) {
        xx = -xx;
      }
      if (IsKeyDown(KEY_UP)) {
        yy += -borderSize * rightStrength;
      }
      if (IsKeyDown(KEY_DOWN)) {
        yy += borderSize * rightStrength;
      }
    } else if (CheckCollisionRecs(ball, top) || ball.y < 0) {
      if (yy < 0) {
        yy = -yy
      }
    } else if (CheckCollisionRecs(ball, bottom) || ball.y > screen.height) {
      if (yy > 0) {
        yy = -yy;
      }
    } else if (ball.x < screen.x) {
      rightScore = rightScore + 1;
      serveBall();
    } else if (ball.x > screen.width) {
      leftScore = leftScore + 1;
      serveBall();
    }
    setBallPos(ball.x + xx * frameTime, ball.y + yy * frameTime);
  }

  let moveRacket(racket: Rectangle, dir: Int): Rectangle {
    var step = if (dir == UP) {
      -borderSize / 2.0
    } else {
      borderSize / 2.0
    };
    step *= frameTime;
    if ((CheckCollisionRecs(top, racket) && dir == UP) || (CheckCollisionRecs(bottom, racket) && dir == DOWN)) {
      return racket;
    } else {
      return new Rectangle(racket.x, racket.y + step, racket.width, racket.height);
    }
  }

  let serveBall(): Void {
    if (yy < 0) {
      yy = borderSize / 2.0;
    } else {
      yy = -borderSize / 2.0;
    }
    setBallPos(border.width / 2.0, GetRandomValue(border.y.toInt() + 10, border.height.toInt()).toFloat64());
  }

  var winnerMessage = "";
  var currentScreen = SCREEN_TITLE;
  while (!WindowShouldClose()) {
    frameTime = GetFrameTime() * 60.0 * gameSpeed;
    match (currentScreen) {
      SCREEN_TITLE -> do {
        if (IsKeyPressed(KEY_ENTER)) {
          currentScreen = SCREEN_PLAY;
        }
      };
      SCREEN_PLAY -> do {
        moveBall();

        if (IsKeyDown(KEY_W)) {
          leftRacket = moveRacket(leftRacket, UP);
        }
        if (IsKeyDown(KEY_S)) {
          leftRacket = moveRacket(leftRacket, DOWN);
        }

        if (IsKeyDown(KEY_UP)) {
          rightRacket = moveRacket(rightRacket, UP);
        }
        if (IsKeyDown(KEY_DOWN)) {
          rightRacket = moveRacket(rightRacket, DOWN);
        }

        if (((leftScore >= maxScore) || (rightScore >= maxScore)) && abs(leftScore - rightScore) > 1) {
          winnerMessage = if (leftScore > rightScore) { "Left" } else { "Right" };
          rightScore = 0;
          leftScore = 0;
          currentScreen = SCREEN_ENDING;
        }
      };
      SCREEN_ENDING -> do {
        if (IsKeyPressed(KEY_ENTER)) {
          currentScreen = SCREEN_TITLE;
        }
      };
    }
    BeginDrawing();
    match (currentScreen) {
      SCREEN_TITLE -> do {
        ClearBackground(BLACK);
        DrawText("PONG", 120, 20, 120, GRAY);
        DrawText("Based on Atari PONG", 120, 140, 60, GRAY);
        DrawText("Programmed with Raylib", 120, 220, 20, GRAY);
        DrawText("Player to the Left: W Key, S Key", 120, 390, 20, GRAY);
        DrawText("Player to the Right: Arrow Up, Arrow Down", 120, 420, 20, GRAY);
        DrawText("Press ENTER to PLAY", 120, 450, 20, GRAY);
        DrawText("Press ESCAPE to QUIT", 120, 480, 20, GRAY);
      };
      SCREEN_PLAY -> do {
        DrawRectangleRec(screen, GRAY);
        DrawRectangle(screen.x.toInt(), border.y.toInt(), screen.width.toInt(), border.height.toInt(), BLACK);
        DrawRectangle(((screen.width/2.0) - 5.0).toInt(), border.y.toInt(), borderSize.toInt(), border.height.toInt(), GRAY);
        DrawText(leftScore.toString(), screen.width.toInt() / 2 - 50 - scoreWidth, 50, 60, GRAY);
        DrawText(rightScore.toString(), screen.width.toInt() / 2 + 50, 50, 60, GRAY);
        DrawRectangleRec(ball, WHITE);
        DrawRectangleRec(leftRacket, WHITE);
        DrawRectangleRec(rightRacket, WHITE);
      };
      SCREEN_ENDING -> do {
        ClearBackground(BLACK);
        DrawText("Player to the ${winnerMessage} Wins!", 120 , 50, 60, GRAY);
        DrawText("Press ENTER to PLAY AGAIN", 120, 420, 20, GRAY);
        DrawText("Press ESCAPE to QUIT", 120, 450, 20, GRAY);
      };
    }
    EndDrawing();
  }
  CloseWindow();
}

